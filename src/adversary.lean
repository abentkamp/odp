import .diff_private .missing_measure .missing_matrix

/-
This file defines the role of the adversary and the ODP algorithm.
-/

open measure_theory ennreal database_type matrix
open_locale ennreal
open_locale matrix

/- `Î©` is a sample space with an associated probability measure `P`. -/
variables {Î© : Type} [measurable_space Î©] (P : measure Î©) [probability_measure P]

/- `O` is the type of outputs of meachanisms. -/
variables (O : Type) [measurable_space O]

/- `X` is a type of databases. -/
variables (X : Type) [database_type X] [measurable_space X]

/-- At each iteration of the algorithm, the adversary can choose an ODP
mechanism and two neighboring databases. The ODP mechanism must not exceed the
remaining Îµ-Î´-budget. -/
structure adversary_choice (Îµ Î´ : â„â‰¥0âˆ) :=
(M : X â†’ Î© â†’ O)
(odp_mechanism : odp_mechanism P M)
(hÎ´ : odp_mechanism.Î´ â‰¤ Î´)
(hÎµ_for : âˆ€ i, odp_mechanism.Îµ_for i â‰¤ Îµ)
(x : fin 2 â†’ X)
(hx : neighboring (x 0) (x 1))

/-- The structure `adversary_n` represents an adversary after `n` iterations.
Given `n` outputs and the remaining Îµ-Î´-budget, the adversary must make a
choice. There a couple of measurability requirements on the adversary
as a function from previous outputs and budget to the adversary's choice. -/
structure adversary_n (n : â„•) :=
(choose : Î  (outputs : fin n â†’ O) (Îµ Î´ : â„â‰¥0âˆ), adversary_choice P O X Îµ Î´)
(measurable_M :
  âˆ€ {Î± : Type} [measurable_space Î±] {os : Î± â†’ (fin n â†’ O)} {Îµ Î´ : Î± â†’ â„â‰¥0âˆ} {x : Î± â†’ X} {Ï‰ : Î± â†’ Î©},
  measurable os â†’ measurable Îµ â†’ measurable Î´ â†’ measurable x â†’ measurable Ï‰ â†’
  measurable (Î» (a : Î±), (choose (os a) (Îµ a) (Î´ a)).M (x a) (Ï‰ a)))
(measurable_x :
  âˆ€ (bit : fin 2) {Î± : Type} [measurable_space Î±] {os : Î± â†’ (fin n â†’ O)} {Îµ Î´ : Î± â†’ â„â‰¥0âˆ},
  measurable os â†’ measurable Îµ â†’ measurable Î´ â†’
  measurable (Î» (a : Î±), (choose (os a) (Îµ a) (Î´ a)).x bit))
(measurable_Îµ :
  âˆ€ {Î± : Type} [measurable_space Î±] {os : Î± â†’ (fin n â†’ O)} {o : Î± â†’ O} {Îµ Î´ : Î± â†’ â„â‰¥0âˆ},
  measurable os â†’ measurable o â†’ measurable Îµ â†’  measurable Î´ â†’
  measurable (Î» (a : Î±), Îµusage (choose (os a) (Îµ a) (Î´ a)).odp_mechanism (o a)))
(measurable_Î´ :
  âˆ€ {Î± : Type} [measurable_space Î±] {os : Î± â†’ (fin n â†’ O)} {Îµ Î´ : Î± â†’ â„â‰¥0âˆ},
  measurable os â†’ measurable Îµ â†’ measurable Î´ â†’
  measurable (Î» (a : Î±), (choose (os a) (Îµ a) (Î´ a)).odp_mechanism.Î´))

/-- An adversary is a collection of `adversary_n` structures for each number of
iterations `n`. -/
def adversary := Î  (n : â„•), adversary_n P O X n

variables {P} {O} {X} (ğ’œ : adversary P O X)

/-- We can produce an adversary for `n` previous outputs from an adversary for
`n + 1` previous outputs by informing them about a new output. -/
def inform_n {n : â„•} (ğ’œ : adversary_n P O X (n+1)) (o : O) : adversary_n P O X n :=
âŸ¨Î» os, ğ’œ.choose (vec_cons o os),
begin
  intros,
  apply ğ’œ.measurable_M (measurable.fin_cons _ _),
  measurability
end,
begin
  intros,
  apply ğ’œ.measurable_x _ (measurable.fin_cons _ _),
  measurability
end,
begin
  intros,
  apply ğ’œ.measurable_Îµ (measurable.fin_cons _ _),
  measurability
end,
begin
  intros,
  apply ğ’œ.measurable_Î´ (measurable.fin_cons _ _),
  measurability
endâŸ©

/-- The adversary `inform ğ’œ o` is `ğ’œ` after having been informed about a new
output `o`. -/
def inform (ğ’œ : adversary P O X) (o : O) : adversary P O X :=
Î» n, inform_n (ğ’œ (n+1)) o

/-- The adversary `inform_vec ğ’œ m os` is `ğ’œ` after having been informed about
`m` new outputs `os`. -/
def inform_vec : adversary P O X â†’ Î  (m : â„•), (fin m â†’ O) â†’ adversary P O X
| ğ’œ 0 os := ğ’œ
| ğ’œ (m+1) os := inform (inform_vec ğ’œ m (vec_butlast os)) (vec_last os)

lemma inform_inform_vec_comm (ğ’œ : adversary P O X) {m : â„•} (os : fin m.succ â†’ O) :
inform (inform_vec ğ’œ m (vec_butlast os)) (vec_last os)
= inform_vec (inform ğ’œ (vec_head os)) m (vec_tail os)
:= begin
  induction m with m ih,
  { refl },
  { unfold inform_vec,
  rw ih,
  rw [head_butlast, last_tail, butlast_tail], }
end

lemma inform_vec_choose (ğ’œ : adversary P O X) {m : â„•} (os : fin m â†’ O) :
(inform_vec ğ’œ m os 0).choose ![] = (ğ’œ m).choose os :=
begin
  induction m with m ih generalizing os ğ’œ,
  { unfold inform_vec, congr' },
  { unfold inform_vec,
    rw inform_inform_vec_comm,
    rw ih,
    unfold inform,
    unfold inform_n,
    unfold adversary_n.choose,
    simp, }
end

lemma inform_inform_vec (ğ’œ : adversary P O X) (m : â„•) (o : O) (os : fin m â†’ O) :
  inform (inform_vec ğ’œ m os) o = inform_vec ğ’œ (m+1) (vec_snoc os o) :=
by rw [inform_vec, butlast_snoc, last_snoc]

/-- This is the main algorithm. -/
noncomputable def odp_composition : Î  (ğ’œ : adversary P O X) (n : â„•) (bit : fin 2) (Îµ Î´ : â„â‰¥0âˆ) (Ï‰s : fin n â†’ Î©), fin n â†’ O
| ğ’œ 0 bit Îµ Î´ Ï‰s := ![]
| ğ’œ (n+1) bit Îµ Î´ Ï‰s :=
-- At each iteration:
  -- The adversary makes a choice.
  let ğ’œ_choice := (ğ’œ 0).choose ![] Îµ Î´ in
  -- We sample the chosen meachanism.
  let o := ğ’œ_choice.M (ğ’œ_choice.x bit) (vec_head Ï‰s) in
  -- We calculate the remaining Îµ-budget.
  let Îµ' := Îµ - Îµusage ğ’œ_choice.odp_mechanism o in
  -- We calculate the remaining Î´-budget.
  let Î´' := Î´ - ğ’œ_choice.odp_mechanism.Î´ in
  -- We inform the adversary about the new output.
  let ğ’œ' := inform ğ’œ o in
  -- We return the output and enter the next iteration for the remaining outputs.
  vec_cons o (odp_composition ğ’œ' n bit Îµ' Î´' (vec_tail Ï‰s))


/-- This auxiliary definition is a fragment of the `odp_composition` algortithm,
but assumes that the current output `o` has already been sampled. -/
noncomputable def odp_compositionâ‚€ (o : O) (n : â„•) (bit : fin 2) (Îµ Î´ : â„â‰¥0âˆ) (Ï‰ : fin n â†’ Î©) :=
  let ğ’œ_choice : adversary_choice P O X Îµ Î´ := (ğ’œ 0).choose ![] Îµ Î´ in
  let Îµ' : â„â‰¥0âˆ := Îµ - Îµusage ğ’œ_choice.odp_mechanism o in
  let Î´' : â„â‰¥0âˆ := Î´ - ğ’œ_choice.odp_mechanism.Î´ in
  let ğ’œ' := inform ğ’œ o in
  odp_composition ğ’œ' n bit Îµ' Î´' Ï‰

/-- The algorithm `odp_composition` is measurable.

Note: I haven't been able to prove this using an adversary that gets fed a list instead of a vector
because lists are currently not instantiated as a measurable space.
-/
lemma measurable_set_odp_composition (bit : fin 2) {n : â„•} {Î± : Type} [measurable_space Î±]
  (m : â„•) (os : Î± â†’ (fin m â†’ O)) (Îµ Î´ : Î± â†’ â„â‰¥0âˆ) (Ï‰ : Î± â†’ (fin n â†’ Î©))
  (hos : measurable os) (hÎµ : measurable Îµ) (hÎ´ : measurable Î´) (hÏ‰ : measurable Ï‰) :
  measurable (Î» a : Î±, odp_composition (inform_vec ğ’œ m (os a)) n bit (Îµ a) (Î´ a) (Ï‰ a)) :=
begin
  induction n with n ih generalizing m Îµ Î´ os,
  case zero { show measurable (Î» Ï‰, ![]), by apply measurable_const },
  case succ { show measurable (Î» a, odp_composition (inform_vec ğ’œ m (os a)) (n+1) bit (Îµ a) (Î´ a) (Ï‰ a)),
    suffices : measurable (Î» a, odp_composition (inform_vec ğ’œ m (os a)) (n+1) bit (Îµ a) (Î´ a) (vec_cons (vec_head (Ï‰ a)) (vec_tail (Ï‰ a)))),
      by simpa only [cons_head_tail] using this,
    unfold odp_composition,
    apply measurable.fin_cons,
    { simp_rw [cons_head_tail, inform_vec_choose ğ’œ],
      apply (ğ’œ m).measurable_M hos hÎµ hÎ´ _ (measurable.comp measurable.vec_head hÏ‰),
      apply (ğ’œ m).measurable_x bit hos hÎµ hÎ´, },
    { simp_rw [inform_inform_vec, matrix.cons_head_tail, inform_vec_choose ğ’œ],
      apply ih (Î» a, vec_tail (Ï‰ a)) _ (m+1),
      apply measurable.vec_snoc,
      exact hos,
      apply (ğ’œ m).measurable_M hos hÎµ hÎ´ _ (measurable.comp measurable.vec_head hÏ‰),
      apply (ğ’œ m).measurable_x bit hos hÎµ hÎ´,
      { apply measurable.sub hÎµ, --TODO: why can't I rewrite inform_vec_choose here?
        suffices : measurable (Î» (a : Î±),
          Îµusage (( ğ’œ m ).choose (os a) (Îµ a) (Î´ a)).odp_mechanism
            (((ğ’œ m).choose (os a) (Îµ a) (Î´ a)).M (((ğ’œ m).choose (os a) (Îµ a) (Î´ a)).x bit) (vec_head (Ï‰ a)))),
        { convert this, apply funext, intro i,
          rw inform_vec_choose ğ’œ (os i) },
        refine (ğ’œ m).measurable_Îµ hos _ hÎµ hÎ´,
        apply (ğ’œ m).measurable_M hos hÎµ hÎ´ _ (measurable.comp measurable.vec_head hÏ‰),
        apply (ğ’œ m).measurable_x bit hos hÎµ hÎ´, },
      { apply measurable.sub hÎ´,
        suffices : measurable (Î» (a : Î±),
          ((ğ’œ m).choose (os a) (Îµ a) (Î´ a)).odp_mechanism.Î´),
        { convert this, apply funext, intro i,
          rw inform_vec_choose ğ’œ (os i) },
        exact (ğ’œ m).measurable_Î´ hos hÎµ hÎ´ },
      exact measurable.comp measurable.vec_tail hÏ‰ } }
end

/-- The algorithm `odp_composition` is measurable. This is a special case of `measurable_set_odp_composition` above. -/
lemma measurable_set_odp_composition' (bit : fin 2) (Îµ Î´ : â„â‰¥0âˆ) {n : â„•}:
  measurable (odp_composition ğ’œ n bit Îµ Î´) :=
begin
  apply measurable_set_odp_composition
    ğ’œ bit 0 (Î»_, ![]) (Î»_, Îµ) (Î»_, Î´) (Î» Ï‰, Ï‰),
  measurability,
end

lemma measurable_odp_compositionâ‚€ (bit : fin 2) (Îµ Î´ : â„â‰¥0âˆ) {n : â„•} :
  measurable (Î» (oÏ‰ : O Ã— (fin n â†’ Î©)), odp_compositionâ‚€ ğ’œ oÏ‰.1 n bit Îµ Î´ oÏ‰.2) :=
begin
  apply measurable_set_odp_composition ğ’œ bit 1
    (Î» oÏ‰ : O Ã— (fin n â†’ Î©), ![oÏ‰.1])
    (Î» oÏ‰ : O Ã— (fin n â†’ Î©), Îµ - Îµusage ((ğ’œ 0).choose vec_empty Îµ Î´).odp_mechanism oÏ‰.fst)
    (Î» oÏ‰ : O Ã— (fin n â†’ Î©), Î´ - ((ğ’œ 0).choose vec_empty Îµ Î´).odp_mechanism.Î´)
    (Î» oÏ‰ : O Ã— (fin n â†’ Î©), oÏ‰.2),
  apply measurable.vec_cons,
  measurability
end